{
  parserClass="cmake.parsing.CMakeParser"
  parserUtilClass="cmake.parsing.CMakeParserUtil"
 
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
 
  psiClassPrefix="CMake"
  psiImplClassSuffix="Impl"
  psiPackage="cmake.psi"
  psiImplPackage="cmake.psi.impl"
 
  elementTypeHolderClass="cmake.psi.CMakeTypes"
  elementTypeClass="cmake.psi.CMakeElementType"
  tokenTypeClass="cmake.psi.CMakeTokenType"
  
  tokens = [
    endfunction="regexp:endfunction"
    function="regexp:function"
    elseiforelse="regexp:elseif|else"
    endif="regexp:endif"
    if="regexp:if"
    endmacro="regexp:endmacro"
    macro="regexp:macro"
    endforeach="regexp:endforeach"
    foreach="regexp:foreach"
    endwhile="regexp:endwhile"
    while="regexp:while"
    keyword="regexp:option|message"
    lpar='('
    rpar=')'
    number='regexp:\d+(\.\d*)?'
    bool='regexp:ON|OFF|TRUE|FALSE'
    bracket_comment = 'regexp:(\#\[=*\[)(.|\n)*?(\]=*\])'
    bracket_argument = 'regexp:(\[=*\[)(.|\n)*?(\]=*\])'
    line_comment='regexp:\#.*'
    var_ref='regexp:\$\{[A-Za-z][A-Za-z0-9_-]*\}'
    escape_identity="regexp:\\\( | \\\) | \\\# | \\\" | (\\ ) | \\\\ | \\\$ | \\\@ | \\\^"
    escape_encoded="regexp:\\t|\\r|\\n"
    escape_semicolon="regexp:\\;"
    quoted_argument='regexp:(")([^"]\\\n|[^"])*(")'
    identifier='regexp:[A-Za-z][A-Za-z0-9_-]*'
     op_and='regexp:AND'
     op_or='regexp:OR'
     op_not='regexp:NOT'
  ]
 }


 file         ::=  file_element* <<eof>>
 file_element ::= !<<eof>> composite  | !<<eof>> command_invocation | line_comment | bracket_comment
 private line_ending  ::=  ( line_comment | bracket_comment )? 
 
 meta compound_expr  ::= <<begin>> file_element* <<end>> { pin=1 }
 meta predicate_expr ::= <<pred>> file_element*
 meta command_expr   ::= <<cmd>> '(' arguments ')' { pin=2 }
 meta seq            ::= <<e>> (<<sep>> <<e>>)*
 
 block ::= <<compound_expr bbegin bend>>
 private functionmacro ::= function | macro
 private endfunctionmacro ::= endfunction | endmacro
 bbegin ::= <<command_expr functionmacro>>
 bend   ::= <<command_expr endfunctionmacro>>
 
 loop   ::= <<compound_expr lbegin lend>>
 lbegin ::= <<command_expr ( foreach | while )>>
 lend   ::= <<command_expr ( endforeach | endwhile )>>
 
 cond   ::= if_expr file_element* <<predicate_expr elseiforelse_expr>>* endif_expr { pin=1 }
 if_expr::= <<command_expr (if)>>
 elseiforelse_expr::=<<command_expr (elseiforelse)>>
 endif_expr::= <<command_expr (endif)>>
 
 composite ::= loop | block | cond
 command_invocation  ::=  <<command_expr command_name>>
 command_name        ::=  keyword | identifier
 
 arguments           ::=  argument? separated_argument*
 separated_argument  ::=  argument | '(' arguments ')'


 argument            ::=  bracket_argument | quoted_argument | unquoted_argument | var_ref
 unquoted_argument   ::=  identifier
 
 
 